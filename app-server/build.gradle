plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.hidetake.ssh' version '2.10.1'
    id 'org.jetbrains.kotlin.jvm'
}

run {
    systemProperty 'pf4j.pluginsDir', './plugins'
}

remotes {
    raspberry {
        host = '192.168.1.100'
        user = 'pi'
        password = 'raspberry'
    }
}

dependencies {
    //common
    implementation project(":modules:data")
    implementation project(":modules:domain")
    implementation project(":modules:actions")
    implementation project(":modules:repository-sqldelight")
    implementation project(":plugin-features:lan-gateway")
    implementation project(":plugin-features:mqtt")

    //pf4j
    implementation "org.pf4j:pf4j:${pf4jVersion}"

    //jetty
    implementation "org.eclipse.jetty:jetty-http:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-io:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-security:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-util:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlets:$jettyVersion"
    implementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"

    //jakarta
    implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    implementation 'jakarta.activation:jakarta.activation-api:2.0.1'

    //jersey + glassfish
    implementation "org.glassfish.jersey.core:jersey-server:$jerseyVersion"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:$jerseyVersion"
    implementation "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-sse:$jerseyVersion"
    implementation "org.glassfish.hk2:hk2-api:$hk2Version"

    //ktor
    implementation "io.ktor:ktor-client-core:$ktorVersion"
    implementation "io.ktor:ktor-client-auth:$ktorVersion"
    implementation "io.ktor:ktor-client-cio:$ktorVersion"
    implementation "io.ktor:ktor-client-gson:$ktorVersion"

    //gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    //slf4j
    implementation "org.slf4j:slf4j-api:2.0.0-alpha5"
    implementation "org.slf4j:slf4j-simple:2.0.0-alpha5"

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
}

shadowJar {
    mainClassName = 'eu.automateeverything.Main'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = "UTF-8"
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.raspberry) {
                execute 'sudo killall -9 java || true'
                execute 'sudo rm -rf /home/pi/ae'
                execute 'mkdir /home/pi/ae'

                put from: "${buildDir}/libs/app-server-all.jar", into: '/home/pi/ae'
                put from: "${buildDir}/../plugins", into: '/home/pi/ae'
                put from: "${buildDir}/../blockly/dist", into: '/home/pi/ae'

                execute 'mv /home/pi/ae/dist/ /home/pi/ae/web/'
            }
        }
    }
}

task startDebugAgent {
    doLast {
        ssh.run {
            session(remotes.raspberry) {
                execute 'sudo screen -dm java -agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005 -Dfile.encoding=UTF-8 -Djava.library.path=/usr/lib/jni -Duser.dir=/home/pi/ae -Dpf4j.pluginsDir=/home/pi/ae/plugins -jar /home/pi/ae/app-server-all.jar'
            }
        }
    }
}